/*
 * Copyright (C) 2017. The UAPI Authors
 * You may not use this file except in compliance with the License.
 * You may obtain a copy of the License at the LICENSE file.
 *
 * You must gained the permission from the authors if you want to
 * use the project into a commercial product
 */

buildscript {
    repositories {
        jcenter()
    }
}

apply from: rootProject.getRootDir().getAbsolutePath() + "/${project_config_file}"
apply plugin: 'jacoco'

def repo = rootProject.ext.base
def subProjects = repo.projects

ext.build_time      = getBuildTime()
ext.publish_repo    = getMavenRepo()

allprojects {
    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    ext.project_version = getBuildVersion(project.name, subProjects)

    group = "${project_group}"
    version = "${project_version}"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        codacy
    }

    dependencies {
        testCompile (
                "org.spockframework:spock-core:${spock_version}"
        )
        testRuntime (
                "cglib:cglib-nodep:${cglib_version}",           // allows mocking of classes (in addition to interfaces)
                "org.objenesis:objenesis:${objenesis_version}"  // allows mocking of classes without default constructor (together with CGLIB)
        )
    }

    jar {
        manifest.attributes
        exclude('**/*.java')
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    jacoco {
        toolVersion = "${jacoco_version}"
    }

    build.dependsOn jacocoTestReport
}

task wrapper(type: Wrapper) {
    gradleVersion = "${gradle_version}"
}

def publishedProjects = subprojects.findAll { it.path != ':uapi.sample' }

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(publishedProjects.test)

    additionalSourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(publishedProjects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(publishedProjects.sourceSets.main.output)
    executionData = files(publishedProjects.jacocoTestReport.executionData)

    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}

configurations {
    codacy
}

dependencies {
    codacy group: 'com.codacy', name: 'codacy-coverage-reporter', version: '1.0.7'
}

task uploadCoverageToCodacy(type: JavaExec) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
    ]
}

def getBuildVersion(projectName, projectMetas) {
    def buildVersion = null
    def matchedMeta = projectMetas.find { it.value.name == projectName }?.value
    if (matchedMeta != null) {
        buildVersion = matchedMeta.version
        if (matchedMeta.type == 'stable') {
            buildVersion = "${buildVersion}-${matchedMeta.type}"
        } else if (matchedMeta.type == 'alpha' || matchedMeta.type == 'beta' || matchedMeta.type == 'rc') {
            buildVersion = "${buildVersion}-${matchedMeta.type}${matchedMeta.no}"
        } else {
            buildVersion = "${buildVersion}-${build_time}"
        }
    } else {
        throw new Exception("No project configuration for project ${projectName}");
    }
    return buildVersion
}

def getBuildTime() {
    def timestampFormat = new java.text.SimpleDateFormat('yyyyMMddHHmmss')
    timestampFormat.timeZone = TimeZone.getTimeZone("UTC")
    return timestampFormat.format(new Date())
}

def getMavenRepo() {
    if (project.project_release.toBoolean()) {
        return 'maven-release'
    } else {
        return 'maven-snapshot'
    }
}
